# --- VST3 Bundle architecture ---
if(NOT VST3_BUNDLE_ARCHITECTURE)
  if(APPLE)
    # VST3 packages are universal on Apple, architecture string not needed
  else()
    # The variable CMAKE_SYSTEM_PROCESSOR is incorrect on Visual studio...
    # see https://gitlab.kitware.com/cmake/cmake/issues/15170
    if(NOT VST3_SYSTEM_PROCESSOR)
      if(MSVC)
        set(VST3_SYSTEM_PROCESSOR "${MSVC_CXX_ARCHITECTURE_ID}")
      else()
        set(VST3_SYSTEM_PROCESSOR "${CMAKE_SYSTEM_PROCESSOR}")
      endif()
    endif()
    # Convert the processor string to VST3 equivalent
    if(VST3_SYSTEM_PROCESSOR MATCHES "^(x86_64|amd64|AMD64|x64|X64)$")
      set(VST3_BUNDLE_ARCHITECTURE "x86_64")
    elseif(VST3_SYSTEM_PROCESSOR MATCHES "^(i.86|x86|X86)$")
      if(WIN32)
        set(VST3_BUNDLE_ARCHITECTURE "x86")
      else()
        set(VST3_BUNDLE_ARCHITECTURE "i386")
      endif()
    elseif(VST3_SYSTEM_PROCESSOR MATCHES "^(armv[3-8][a-z]*)$")
      set(VST3_BUNDLE_ARCHITECTURE "${VST3_SYSTEM_PROCESSOR}")
    elseif(VST3_SYSTEM_PROCESSOR MATCHES "^(aarch64)$")
      set(VST3_BUNDLE_ARCHITECTURE "aarch64")
    else()
      message(FATAL_ERROR "We don't know this architecture for VST3: ${VST3_SYSTEM_PROCESSOR}.")
    endif()
    message(STATUS "The VST3 architecture is deduced as: ${VST3_BUNDLE_ARCHITECTURE}")
  endif()
endif()
